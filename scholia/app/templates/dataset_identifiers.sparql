PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?Identifier ?IdentifierLabel
  ?id (SAMPLE(?idUrls) as ?idUrl)
  ?IdentifierDescription
WITH {
  SELECT ?Identifier ?id ?formatterurl WHERE {
    target: ?IDdir ?id .
    ?Identifier wikibase:directClaim ?IDdir ;
            wdt:P31/wdt:P279* wd:Q19847637 .
    OPTIONAL {
      ?Identifier wdt:P1630 ?formatterurl .
    }
  } LIMIT 500
} AS %RESULTS {
  { SELECT * WHERE {
      INCLUDE %RESULTS
      FILTER (?Identifier = wd:P234)
      BIND(IRI(REPLACE(?formatterurl, '\\\\$1', ENCODE_FOR_URI(str(?id)))) AS ?idUrls).
    }
  } UNION {
    SELECT * WHERE {
      INCLUDE %RESULTS
      FILTER (?Identifier != wd:P234)
      BIND(IRI(REPLACE(?formatterurl, '\\\\$1', str(?id))) AS ?idUrls).
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?Identifier ?IdentifierLabel ?IdentifierDescription ?id
ORDER BY ASC(?IdentifierLabel)
